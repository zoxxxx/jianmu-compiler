### 程序
```
Program → CompUnit
```
### 编译单元
```
CompUnit → CompUnit Decl | CompUnit FuncDef | Decl | FuncDef
```

### 声明
```
Decl → ConstDecl | VarDecl
```

### 常量声明
```
ConstDecl → 'const' BType ConstDefList ';'
ConstDefList → ConstDef | ConstDefList ',' ConstDef
```

### 基本类型
```
BType → 'int' | 'float'
```

### 常数定义
```
ConstDef → Ident ConstExpList '=' ConstInitVal
ConstExpList → ε | ConstExpList '[' ConstExp ']'
```

### 常量初值
```
ConstInitVal → ConstExp | '{' ConstInitValList '}'
ConstInitValList → ε | ConstInitVal | ConstInitValList ',' ConstInitVal
```

### 变量声明
```
VarDecl → BType VarDefList ';'
VarDefList → VarDef | VarDefList ',' VarDef
```

### 变量定义
```
VarDef → Ident ConstExpList | Ident ConstExpList '=' InitVal
```

### 变量初值
```
InitVal → Exp | '{' InitValList '}'
InitValList → ε | InitVal | InitValList ',' InitVal
```

### 函数定义
```
FuncDef → FuncType Ident '(' FuncFParams ')' Block | FuncType Ident '(' ')' Block
```

### 函数类型
```
FuncType → 'void' | 'int' | 'float'
```

### 函数形参表
```
FuncFParams → FuncFParam | FuncFParams ',' FuncFParam
```

### 函数形参
```
FuncFParam → BType Ident | BType Ident '[' ']' ExpList
ExpList → ε | ExpList '[' Exp ']'
```

### 语句块
```
Block → '{' BlockItemList '}'
BlockItemList → ε | BlockItemList BlockItem
```

### 语句块项
```
BlockItem → Decl | Stmt
```

### 语句
```
Stmt → LVal '=' Exp ';'
    | ';' | Exp ';'
    | Block
    | 'if' '(' Cond ')' Stmt
    | 'if' '(' Cond ')' Stmt 'else' Stmt
    | 'while' '(' Cond ')' Stmt
    | 'break' ';' | 'continue' ';'
    | 'return' ';'
    | 'return' Exp ';'
```

### 表达式
```
Exp → AddExp
```

### 条件表达式
```
Cond → LOrExp
```

### 左值表达式
```
LVal → Ident ExpList
```

### 基本表达式
```
PrimaryExp → '(' Exp ')' | LVal | Number
```

### 数值
```
Number → IntConst | FloatConst
```

### 一元表达式
```
UnaryExp → PrimaryExp | Ident '(' ')' | Ident '(' FuncRParams ')' | UnaryOp UnaryExp
```

### 单目运算符
```
UnaryOp → '+' | '−' | '!'
```

### 函数实参表
```
FuncRParams → Exp | FuncRParams ',' Exp
```

### 乘除模表达式
```
MulExp → UnaryExp | MulExp '*' UnaryExp | MulExp '/' UnaryExp | MulExp '%' UnaryExp
```

### 加减表达式
```
AddExp → MulExp | AddExp '+' MulExp | AddExp '−' MulExp
```

### 关系表达式
```
RelExp → AddExp | RelExp '<' AddExp | RelExp '>' AddExp | RelExp '<=' AddExp | RelExp '>=' AddExp
```

### 相等性表达式
```
EqExp → RelExp | EqExp '==' RelExp | EqExp '!=' RelExp
```

### 逻辑与表达式
```
LAndExp → EqExp | LAndExp '&&' EqExp
```

### 逻辑或表达式
```
LOrExp → LAndExp | LOrExp '||' LAndExp
```

### 常量表达式
```
ConstExp → AddExp
```